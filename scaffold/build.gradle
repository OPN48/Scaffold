apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.xuqingquan1995'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner rootProject.ext.android.AndroidJUnitRunner
        flavorDimensions 'default'
    }
    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            jniLibs.srcDir 'libs'//说明so的路径为该libs路径，关联所有so文件
        }
    }
    dexOptions {
        preDexLibraries true
        maxProcessCount 8
    }
    //如果有用到kapt添加如下配置
    kapt {
        useBuildCache = true
        javacOptions {
            option("-Xmaxerrs", 500)
        }
    }
}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')
    api rootProject.ext.deps.kotlin
    api rootProject.ext.deps.kotlinx_coroutines_core
    api rootProject.ext.deps.kotlinx_coroutines_android
    api rootProject.ext.deps.appcompat
    api rootProject.ext.deps.constraintlayout
    api rootProject.ext.deps.core_ktx
    api rootProject.ext.deps.lifecycle
    api rootProject.ext.deps.lifecycle_runtime_ktx
    api rootProject.ext.deps.anko
    compileOnly rootProject.ext.deps.multidex
    //view
    api rootProject.ext.deps.recyclerview
    api rootProject.ext.deps.exifinterface
    compileOnly rootProject.ext.deps.paging
    api rootProject.ext.deps.glide
    kapt rootProject.ext.deps.glide_compiler
    api rootProject.ext.deps.material
    compileOnly rootProject.ext.deps.autosize
    //retorfit
    api (rootProject.ext.deps.retrofit){
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    api rootProject.ext.deps.okhttp
    api rootProject.ext.deps.converter_gson
    api (rootProject.ext.deps.logging_interceptor){
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
    }
    //format
    api rootProject.ext.deps.gson
    //eventbus
    api rootProject.ext.deps.eventbus
    //download（需要用到web下载的时候需要依赖）
    compileOnly rootProject.ext.deps.Downloader
    //debug
    debugApi rootProject.ext.deps.leakcanary
    debugApi rootProject.ext.deps.recovery
    debugApi rootProject.ext.deps.anrwatchdog
    releaseCompileOnly rootProject.ext.deps.recovery
    releaseCompileOnly rootProject.ext.deps.anrwatchdog
    //test
    testImplementation rootProject.ext.deps.junit
    androidTestImplementation rootProject.ext.deps.espresso_core
    androidTestImplementation rootProject.ext.deps.test_rules
    androidTestImplementation rootProject.ext.deps.test_ext
}
